//Recreate abilities table

/*
This component updates ONLY the abilities table of the kit(s)
It cannot change which kits are installed
If "Use config file" was chosen, this will use the current file in the mod folder
*/


ACTION_IF (MOD_IS_INSTALLED ~kitmule.tp2~ (ID_OF_LABEL ~kitmule.tp2~ ~D2_KIT_MULE~)) BEGIN 
  COPY - ~%MOD_FOLDER%/config.ini~ ~%workspace%~ 
END

ACTION_IF (MOD_IS_INSTALLED ~kitmule.tp2~ (ID_OF_LABEL ~kitmule.tp2~ ~D2_KIT_MULE_DEFAULT~)) BEGIN 
  COPY - ~%files%/config~ ~%workspace%/config.ini~ 
END



INCLUDE ~%install%/config.tph~
INCLUDE ~%install%/abilities.tph~

ACTION_IF (FILE_EXISTS ~override/%kitid%b.2DA~) BEGIN
  COPY_EXISTING ~%kitid%.2DA~  ~override/%kitid%b.2DA~
    PATCH_IF (GAME_IS ~iwdee~) BEGIN 
      LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~#BARD-1~ END
    END
    ELSE BEGIN 
      LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~FJBARD~ END
    END
END

ACTION_IF (FILE_EXISTS ~override/%kitid%d.2DA~) BEGIN
  COPY_EXISTING ~%kitid%.2DA~  ~override/%kitid%d.2DA~
    PATCH_IF (GAME_IS ~iwdee~) BEGIN 
      LPF d2_kit_ability_adder INT_VAR level=15 passive=1 STR_VAR resource=~SPCL939~ END
      LPF d2_kit_ability_adder INT_VAR level=9 passive=1 STR_VAR resource=~SPCL924~ END
    END
    ELSE BEGIN 
      LPF d2_kit_ability_adder INT_VAR level=15 passive=1 STR_VAR resource=~SPCL924~ END
      LPF d2_kit_ability_adder INT_VAR level=18 passive=1 STR_VAR resource=~SPCL925~ END
      LPF d2_kit_ability_adder INT_VAR level=21 passive=1 STR_VAR resource=~SPCL926~ END
      LPF d2_kit_ability_adder INT_VAR level=24 passive=1 STR_VAR resource=~SPCL927~ END
    END
END



//Ends current run of WeiDU immediately after this component installs
//Just adds a marker file to override, then uses command line to delete it at exit

COPY ~%files%/config~ ~override/d2marker~

ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL_CASE ~win32~) BEGIN
  AT_EXIT ~del /f /q override\d2marker >nul 2>&1~
END
ELSE BEGIN
  AT_EXIT ~rm -f override/d2marker >/dev/null 2>&1~
END


/*
This component does not write to weidu.log. 
This means it cannot be uninstalled, but it doesn't need to be.
This is more of an update component for the 2da file added by the main component.
If the main component is uninstalled, any files it adds are removed.
*/