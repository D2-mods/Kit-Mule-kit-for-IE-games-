BACKUP ~weidu_external/backup/kitmule~
AUTHOR ~Dan_P~
VERSION ~1.2~
AUTO_EVAL_STRINGS

ALWAYS
  ACTION_BASH_FOR ~%MOD_FOLDER%/lib~ ~.*\.tpa~ BEGIN    // function list
    INCLUDE ~%BASH_FOR_FILESPEC%~
  END

  ACTION_IF !(DIRECTORY_EXISTS ~weidu_external/Workspace~) BEGIN    // make folder for edits
    MKDIR ~weidu_external/Workspace~
  END
  
  ACTION_DEFINE_ARRAY d2noconvert BEGIN setup END    // tra stuff for classic games
  ACTION_DEFINE_ARRAY d2reload BEGIN main END
  LAF HANDLE_CHARSETS
    INT_VAR
      from_utf8        = 1
      infer_charsets   = 1
    STR_VAR
      default_language = ~english~
      tra_path         = EVAL ~%MOD_FOLDER%/tra~
      out_path         = EVAL ~weidu_external/lang/%MOD_FOLDER%~
      noconvert_array  = ~d2noconvert~
      reload_array     = ~d2reload~
  END

  OUTER_TEXT_SPRINT files      ~%MOD_FOLDER%/files~    // folder variables
  OUTER_TEXT_SPRINT install    ~%MOD_FOLDER%/install~
  OUTER_TEXT_SPRINT workspace  ~weidu_external/Workspace~

  CLEAR_INLINED
END

LANGUAGE ~English~
         ~english~
         ~kitmule/tra/english/setup.tra~
         ~kitmule/tra/english/main.tra~
		 
LANGUAGE ~French~
         ~french~
         ~kitmule/tra/english/setup.tra~
         ~kitmule/tra/english/main.tra~
         ~kitmule/tra/french/setup.tra~
         ~kitmule/tra/french/main.tra~
		

////////////////////////////////////////////////////////////

//Recreate abilities table (requires main component)

BEGIN @99
DESIGNATED 99
REQUIRE_PREDICATE ((FILE_EXISTS ~override/d2kmule.SPL~) AND 
                   (FILE_EXISTS ~override/d2kmule.2DA~)) ~~
NO_LOG_RECORD
LABEL ~D2_KIT_MULE_RECREATE~
INCLUDE ~%install%/recreate.tph~

////////////////////////////////////////////////////////////

//Kit Mule kit
		 
BEGIN @11    // Use config file
DESIGNATED 9999
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee iwdee eet bg2 tob tutu tutu_totsc iwd_in_bg2 ca~) ~Game not supported~
REQUIRE_PREDICATE !(FILE_EXISTS ~override/d2marker~) ~~
SUBCOMPONENT @10
LABEL ~D2_KIT_MULE~
COPY - ~%MOD_FOLDER%/config.ini~ ~%workspace%~
INCLUDE ~%install%/install.tph~

BEGIN @12    // Use default settings
REQUIRE_PREDICATE !(FILE_EXISTS ~override/d2marker~) ~~
SUBCOMPONENT @10
LABEL ~D2_KIT_MULE_DEFAULT~
COPY - ~%files%/config~ ~%workspace%/config.ini~
INCLUDE ~%install%/install.tph~

////////////////////////////////////////////////////////////



/*
*/